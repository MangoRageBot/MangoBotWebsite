import org.mangorage.mangobotgradle.util.GitVersion


buildscript {
    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
        maven {
            url = 'https://repo.mattmalec.com/repository/releases'
        }
        maven {
            url = "https://maven.mangorage.org/releases"
        }
    }

    dependencies {
        classpath 'org.mangorage:MangoBotGradle:6.0.14'
    }
}

plugins {
    id 'org.gradlex.extra-java-module-info' version '1.12'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'MangoBotGradle'

group = 'org.mangorage'
version = GitVersion.getGitVersion().getVersionAsString()

println("Version: " + version)

MangoBotConfig {
    setJarTask(jar)

    addRunConfig {
        it.setName("runDevBot")
        it.addArgs("--launchTarget", "mangobot")
        it.addArgs("--dev")
    }
}

extraJavaModuleInfo {
    setDeriveAutomaticModuleNamesFromFileNames(true)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor {
        languageVersion.set(JavaLanguageVersion.of(24))
    })
}

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url = 'https://maven.minecraftforge.net/'
    }

    maven {
        url = "https://maven.mangorage.org/releases"
    }

    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url "https://maven.lavalink.dev/releases/"
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    installer('org.mangorage:installer:4.0.20')
    bootstrap("org.mangorage:mangobotbootstrap:1.0.46")

    plugin('org.mangorage:mangobot:12.0.72')
    plugin('org.mangorage:mangobotplugin:12.0.41')

    library('org.eclipse.jetty:jetty-server:11.0.16')
    library('org.eclipse.jetty:jetty-servlet:11.0.16')
    library('org.eclipse.jetty:jetty-security:11.+')
    library('org.eclipse.jetty:jetty-util:11.0.16')
    library('org.freemarker:freemarker:2.3.31')
}

configurations.all {
    resolutionStrategy {
        force 'com.google.code.gson:gson:2.11.+'
    }
}

test {
    useJUnitPlatform()
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            mangobot_version: version
    ]
    inputs.properties replaceProperties

    filesMatching(['mangobotsite.plugin.json']) {
        expand replaceProperties + [project: project]
    }
}

publishing {
    publications.register("mangobotaddon", MavenPublication) {
        artifact jar

        pom {
            name = 'MangoBot Website Plugin'
            description = 'The Website Plugin for MangoBot'
            url = 'https://github.com/MangoRageBot/MangoBotWebsite'
            issueManagement {
                system = 'github'
                url = 'https://github.com/MangoRageBot/MangoBotWebsite/issues'
            }
            developers {
                developer {
                    id = 'mangorage'
                    name = 'MangoRage'
                }
            }
            withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.library.getDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.group)
                    dependency.appendNode('artifactId', it.name)
                    dependency.appendNode('version', it.version)
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://maven.mangorage.org/releases"

            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}